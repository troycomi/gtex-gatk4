chroms = list(range(1,23))

target_ids = [
    'LP6005441-DNA_A04',
    'LP6005441-DNA_B11',
    'LP6005441-DNA_F01'
]

if "subw_outputs_dict" in locals() and "ids" in locals():
    subw_outputs_dict['rehead_and_split_bam'].extend(
         expand(paths['chroms_bam'],
                id=target_ids,
                chromosome=chroms))

rule rehead_top_bam:
    input:
        bam=paths['base_bam'],
        bai=paths['base_bai']

    output:
        bam=temp(paths['rehead_bam']),
        bai=temp(paths['rehead_bai']),

    singularity:
        paths['gatk-container']

    shell:
        'samtools view -H {input.bam} |'
            'sed \'{config[sed_command]}\' |'
            'samtools reheader - {input.bam} > {output.bam} \n'
        'samtools index {output.bam} {output.bai}'

rule rehead_split_bam:
    input:
        bam=paths['rehead_bam'],
        bai=paths['rehead_bai'],

    output:
        bam=temp(paths['chroms_temp_bam']),
        bai=temp(paths['chroms_temp_bai'])

    singularity:
        paths['gatk-container']

    shell:
        'samtools view -hb {input.bam} {wildcards.chromosome} > {output.bam}.temp \n'
        'samtools view -H {output.bam}.temp |'
            'sed \'/SN:M/d\' | '  # remove mismatched sizes not needed
            'samtools reheader - {output.bam}.temp > {output.bam} \n'
        'rm {output.bam}.temp \n'
        'samtools index {output.bam} {output.bai} '

rule rehead_add_sample:
    input:
        bam=paths['chroms_temp_bam'],
        bai=paths['chroms_temp_bai']

    output:
        bam=paths['chroms_bam'],
        bai=paths['chroms_bai']

    singularity:
        paths['gatk-container']

    #@RG     ID:LP6005441-DNA_F02    SM:LP6005441-DNA_F02
    # add in RG line to header
    shell:
        '{{ samtools view -H {input.bam} ;'
            ' echo -e "@RG\\tID:{wildcards.id}\\tSM:{wildcards.id}" ; }} | '
            'samtools reheader - {input.bam} > {output.bam} \n'
        'samtools index {output.bam} {output.bai} '
