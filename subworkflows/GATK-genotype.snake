map_ids = [
    'LP6005441-DNA_A04',
    'LP6005441-DNA_B11',
    'LP6005441-DNA_F01'
]

if "subw_outputs_dict" in locals() and "ids" in locals():
    subw_outputs_dict['GATK-genotype'].extend(
         expand(paths['haplotype_raw'],
                chromosome=chromosomes,
                id=map_ids))
         #id=ids.keys()))
    # subw_outputs_dict['GATK-genotype'].extend(
    #     expand(paths['indel_vcf'],
    #            chromosome=chromosomes))

localrules:
    name_map

rule index_bam:
    input: ancient("{dir}/{id}.chr{chromosome}.bam")
    output: "{dir}/{id}.chr{chromosome}.bam.bai"

    singularity:
        paths['gatk-container']

    resources:
        mem=lambda wildcards, input, attempt: est_resource(
                'index_bam', input, 'mem', 4000, attempt),
        time=lambda wildcards, input, attempt: est_resource(
                'index_bam', input, 'time', 90, attempt),
        short_jobs=lambda wildcards, input, attempt: est_resource(
                'index_bam', input, 'short_jobs', 0, attempt),

    shell:
        'samtools sort -m 3G {input} {input}.sort \n'
        'mv {input}.sort.bam {input} \n'
        'samtools index {input} {output} '

def haplotype_input(wildcards):
    return {
        'bam': ancient(ids[wildcards.id].format(
            id=wildcards.id, chromosome=wildcards.chromosome)),
        'bai': ancient((ids[wildcards.id] + '.bai').format(
            id=wildcards.id, chromosome=wildcards.chromosome)),
            }


rule haplotype_caller:
    input:
        unpack(haplotype_input)

    output:
        vcf=paths['haplotype_raw'],
        idx=paths['haplotype_idx']

    resources:
        mem=lambda wildcards, input, attempt: est_resource(
                'haplotype', input, 'mem', 8000, attempt),
        time=lambda wildcards, input, attempt: est_resource(
                'haplotype', input, 'time', 360, attempt),
        short_jobs=lambda wildcards, input, attempt: est_resource(
                'haplotype', input, 'short_jobs', 0, attempt),

    singularity:
        paths['gatk-container']

    shell:
        'gatk --java-options "-Xmx{config[gatk][HaplotypeCaller][memory]}" '
            'HaplotypeCaller  '
            '--verbosity {config[gatk][verbosity]} '
            '-R {paths[ref_genome]} '
            '-I {input.bam} '
            '-O {output.vcf} '
            '{config[gatk][HaplotypeCaller][options]}'

rule name_map:
    input:
        vcf=lambda wildcards: expand(paths['haplotype_raw']\
                                     .replace('{chromosome}', \
                                              wildcards.chromosome),
                                     id=ids),
        idx=lambda wildcards: expand(paths['haplotype_idx']\
                                     .replace('{chromosome}', \
                                              wildcards.chromosome),
                                     id=ids)

    output:
        temp(paths['name_map'])

    run:
        import glob
        import os
        with open(output[0], 'w') as outfile:
            # get variants in output
            for f in input.vcf:
                sample = os.path.split(f)[1]
                sample = sample[:sample.find('.')]  # remove after first _
                outfile.write(
                    "{}\t{}\n".format(
                        sample,
                        f))

            # get variants in 1000 genomes
            supp_dir = paths['1000G_dir'].replace('{chromosome}',
                                                  wildcards.chromosome)

            for f in glob.glob(supp_dir + "/*.g.vcf"):
                sample = os.path.split(f)[1]
                sample = sample[:sample.find('_')]  # remove after first _
                outfile.write(
                    "{}\t{}\n".format(
                        sample,
                        f))

rule db_import:
    input:
        paths['name_map']

    output:
        temp(directory(paths['genomics_db']))

    singularity:
        paths['gatk-container']

    shell:
        'gatk --java-options "-Xmx{config[gatk][GenomicsDBImport][memory]}" '
            'GenomicsDBImport  '
            '--verbosity {config[gatk][verbosity]} '
            '-L {wildcards.chromosome} '
            '--sample-name-map {input} '
            '--genomicsdb-workspace-path {output} '
            '{config[gatk][GenomicsDBImport][options]} '

rule genotype_gvcf:
    input:
        db=paths['genomics_db'],

    output:
        vcf=paths['genotype_vcf'],
        tbi=paths['genotype_tbi']

    params:
        interval=lambda wildcards: f'{wildcards.chromosome}'

    singularity:
        paths['gatk-container']

    group: 'genotype_gvcf'

    shell:
        'gatk --java-options "-Xmx{config[gatk][GenotypeGVCFs][memory]}" '
            'GenotypeGVCFs '
            '--verbosity {config[gatk][verbosity]} '
            '-R {paths[ref_genome]} '
            '-L {params.interval} '
            '--variant gendb://{input.db} '
            '-O {output.vcf} '
            '{config[gatk][GenotypeGVCFs][options]} '
            '2>/dev/null'

def get_snp_options(wildcards):
    return config['gatk']['VariantRecalibrator']['options'].format(
            hapmap=paths['ref_hapmap'],
            omni=paths['ref_omni'],
            OKG=paths['ref_1000g'],
            dbsnp=paths['ref_dbsnp'])

rule variant_recal_snp:
    input:
        ref=paths['ref_genome'],
        vcf=paths['genotype_vcf'],

    output:
        recal=paths['snp_recal'],
        tranche=paths['snp_tranche'],
        rscript=paths['snp_script']

    params:
        get_snp_options

    singularity:
        paths['gatk-container']

    shell:
        'gatk --java-options "-Xmx{config[gatk][VariantRecalibrator][memory]}" '
            'VariantRecalibrator '
            '--verbosity {config[gatk][verbosity]} '
            '-R {input.ref} '
            '-V {input.vcf} '
            '--output {output.recal} '
            '--tranches-file {output.tranche} '
            '--rscript-file {output.rscript} '
            '{params}'

rule apply_vqsr_snp:
    input:
        ref=paths['ref_genome'],
        vcf=paths['genotype_vcf'],
        recal=paths['snp_recal'],
        tranche=paths['snp_tranche'],

    output:
        vcf=paths['snp_vcf']

    singularity:
        paths['gatk-container']

    shell:
        'gatk --java-options "-Xmx{config[gatk][ApplyVQSR][memory]}" '
            'ApplyVQSR '
            '--verbosity {config[gatk][verbosity]} '
            '-R {input.ref} '
            '--tranches-file {input.tranche} '
            '--recal-file {input.recal} '
            '-V {input.vcf} '
            '-O {output.vcf} '
            '{config[gatk][ApplyVQSR][options]}'

def get_indel_options(wildcards):
    return config['gatk']['VariantRecalibratorIndel']['options'].format(
            mills=paths['ref_mills'],
            dbsnp=paths['ref_dbsnp'])

rule variant_recal_indel:
    input:
        ref=paths['ref_genome'],
        vcf=paths['snp_vcf'],

    output:
        recal=paths['indel_recal'],
        tranche=paths['indel_tranche'],
        rscript=paths['indel_script']

    params:
        get_indel_options

    singularity:
        paths['gatk-container']

    shell:
        'gatk --java-options "-Xmx{config[gatk][VariantRecalibrator][memory]}" '
            'VariantRecalibrator '
            '--verbosity {config[gatk][verbosity]} '
            '-R {input.ref} '
            '-V {input.vcf} '
            '--output {output.recal} '
            '--tranches-file {output.tranche} '
            '--rscript-file {output.rscript} '
            '{params}'

rule apply_vqsr_indel:
    input:
        ref=paths['ref_genome'],
        vcf=paths['snp_vcf'],
        recal=paths['indel_recal'],
        tranche=paths['indel_tranche'],

    output:
        vcf=paths['indel_vcf']

    singularity:
        paths['gatk-container']

    shell:
        'gatk --java-options "-Xmx{config[gatk][ApplyVQSR][memory]}" '
            'ApplyVQSR '
            '--verbosity {config[gatk][verbosity]} '
            '-R {input.ref} '
            '--tranches-file {input.tranche} '
            '--recal-file {input.recal} '
            '-V {input.vcf} '
            '-O {output.vcf} '
            '{config[gatk][ApplyVQSRIndel][options]}'
