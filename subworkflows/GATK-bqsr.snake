from scripts.clean_config import join_config_paths

map_ids = [
    'LP6005441-DNA_A04',
    'LP6005441-DNA_B11',
    'LP6005441-DNA_F01'
]

if "subw_outputs_dict" in locals() and "ids" in locals():
    subw_outputs_dict['GATK-bqsr'] = expand(paths['recal_covariates'], id=map_ids)

if 'rehead_bam' not in paths:
    configfile: paths['base'] + '/subworkflows/GATK-map.yaml'
    paths = join_config_paths(paths, config['path'])

def get_bqsr_input_bams(wildcards):
    return [paths['rehead_bam'].format(id=wildcards.id),
            paths['rehead_bai'].format(id=wildcards.id),
            ]
def get_bqsr_mem(wildcards, input):
    return est_resource('bqsr', get_bqsr_input_bams(wildcards), 'mem', 6000)

def get_bam(wildcards):
    if wildcards.type == 'before':
        return {'bam': ancient(paths['rehead_bam']), 'bai': ancient(paths['rehead_bai'])}
    elif wildcards.type == 'after':
        return {'bam': ancient(paths['recal_bam'])}
    else:
        raise ValueError("Unknown bqsr table type {}".format(wildcards.type))

def get_table_options(wildcards):
    return config['gatk']['BQSR']['known_sites'].format(
            dbsnp=paths['ref_dbsnp'],
            mills=paths['ref_mills'],
            indel=paths['ref_indel'],
            )

rule bqsr_table:
    input:
        unpack(get_bam)

    output:
        paths['recal_table']

    params:
        get_table_options 

    singularity:
        paths['gatk-container']

    group: 'bqsr'

    resources:
        mem=get_bqsr_mem,

    shell:
        'gatk --java-options "-Xmx$(({resources.mem} * 9 / 10))M" '
            'BaseRecalibrator '
            '-R {paths[ref_genome]} '
            '--verbosity {config[gatk][verbosity]} '
            '-I {input.bam} '
            '-O {output} '
            '{params} '
            # '-L {paths[exome_intervals]} '

def get_before_table(wildcards):
    return {'bam': ancient(paths['rehead_bam']),
            'bai': ancient(paths['rehead_bai']),
            'table': ancient(paths['recal_table'].replace('{type}', 'before'))}

rule bqsr_bam:
    input:
        unpack(get_before_table)

    output:
        bam=protected(paths['recal_bam'])

    singularity:
        paths['gatk-container']

    group: 'bqsr'

    resources:
        mem=get_bqsr_mem,

    shell:
        'gatk --java-options "-Xmx$(({resources.mem} * 9 / 10))M" '
            'ApplyBQSR '
            '-R {paths[ref_genome]} '
            '--verbosity {config[gatk][verbosity]} '
            '-bqsr {input.table} '
            '-I {input.bam} '
            '-O {output.bam}'

def get_tables(wildcards):
    return {'before': ancient(paths['recal_table'].replace('{type}', 'before')),
            'after': ancient(paths['recal_table'].replace('{type}', 'after'))}

rule analyze_bqsr:
    input:
        unpack(get_tables)

    output:
        paths['recal_covariates']

    singularity:
        paths['gatk-container']

    group: 'bqsr'

    resources:
        mem=get_bqsr_mem,
        time=lambda wildcards, input: est_resource('bqsr', get_bqsr_input_bams(wildcards), 'time', 24*60),
        short_jobs=lambda wildcards, input: est_resource('bqsr', get_bqsr_input_bams(wildcards), 'short_jobs', 0),

    shell:
        'gatk --java-options "-Xmx$(({resources.mem} * 9 / 10))M" '
            'AnalyzeCovariates '
            '--verbosity {config[gatk][verbosity]} '
            '-before {input.before} '
            '-after {input.after} '
            '-plots {output}'
