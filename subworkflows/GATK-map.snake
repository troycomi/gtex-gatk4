if "subw_outputs_dict" in locals() and "ids" in locals():
    subw_outputs_dict['GATK-map'] = expand(paths['dedup_bam'], id=ids)

ruleorder: fastq_to_ubam > revert_sam

rule revert_sam:
    input:
        ancient(paths['old_bam'])

    output:
        temp(paths['ubam'])

    singularity:
        paths['gatk-container']

    group: 'map_fastq'

    shell:
        'gatk --java-options "-Xmx{config[gatk][RevertSam][memory]}" '
            'RevertSam '
            '--VERBOSITY {gatk[verbosity]} '
            '--TMP_DIR={path[temp_dir]} '
            '-I={input} '
            '-O={output} '
    
rule fastq_to_ubam:
    input:
        R1=ancient(paths['fastq_R1']),
        R2=ancient(paths['fastq_R2'])

    output:
        temp(paths['ubam'])

    singularity:
        paths['gatk-container']

    resources:
        fastq_instances=1

    shell:
        'zcat {input.R1} | '
            'python {paths[base]}/scripts/split_fastq.py \
                {input.R1} {output} & \n'
        'zcat {input.R2} | '
            'python {paths[base]}/scripts/split_fastq.py \
                {input.R2} {output} & \n'
        'wait \n'
        'RG1=($(python {paths[base]}/scripts/get_fastq.py \
            {input.R1} {output} )) \n'
        'RG2=($(python {paths[base]}/scripts/get_fastq.py \
            {input.R2} {output} )) \n'
        'TEMP_UBAMS=() \n'
        'UBAMS_ARG=() \n'
        'for i in ${{!RG1[@]}}; do \n'
            'TEMP_UBAM=$(dirname ${{RG1[$i]}})/$(basename ${{RG1[$i]}} | '  
                'cut -d "_" -f 1-4).ubam  \n'
            'RG=$(basename ${{RG1[$i]}} | cut -d "_" -f 2,3,4 --output-delimiter=".")  \n'
            'gatk --java-options "-Xmx{config[gatk][FastqToSam][memory]}" '
                'FastqToSam '
                '-F1=${{RG1[$i]}} '
                '-F2=${{RG2[$i]}} '
                '-SM={wildcards.id} '
                '-RG=${{RG}}.{wildcards.id} '
                '-PU=${{RG}}.{wildcards.id} '
                '-O=$TEMP_UBAM '
                '--VERBOSITY={config[gatk][verbosity]} '
                '{config[gatk][FastqToSam][options]} \n'
            'TEMP_UBAMS+=($TEMP_UBAM)  \n'
            'UBAMS_ARG+=(-I $TEMP_UBAM)  \n'
        'done  \n'
        'gatk --java-options "-Xmx{config[gatk][MergeSamFiles][memory]}" '
            'MergeSamFiles '
            '{config[gatk][MergeSamFiles][options]} '
            '--VERBOSITY={config[gatk][verbosity]} '
            '-O {output} '
            '${{UBAMS_ARG[*]}} \n'
        'rm ${{RG1[@]}} ${{RG2[@]}}  \n'
        'rm ${{TEMP_UBAMS[*]}}'

def get_split_names(wildcards):
    '''
    return prefix and suffix for split output
    '''
    filename = paths['split_ubam'].replace('{id}', wildcards.id)
    prefix, suffix = filename.split('{part}')
    return {'prefix': prefix, 'suffix': suffix}

checkpoint split_ubam:
    input:
        paths['ubam']

    output:
        directory(paths['split_ubam_dir'])

    params:
        splitnames=get_split_names
        
    singularity:
        paths['gatk-container']

    # make output dir, store header, split sam and convert back to bam
    shell:
        "mkdir {output}\n"
        "samtools view -H {input} > {output}/header.sam\n"
        "samtools view {input} | "
        "split "
            "--lines={config[map_split_lines]} "
            "--additional-suffix={params.splitnames[suffix]} "
            "--filter='cat {output}/header.sam - | "
                "samtools view -Sb - > $FILE' "
            "- {params.splitnames[prefix]}\n"
        "rm {output}/header.sam"


rule mark_adapters:
    input:
        paths['split_ubam']

    output:
        bam=temp(paths['marked_bam']),
        metric=temp(paths['marked_metric'])

    singularity:
        paths['gatk-container']

    shell:
        'gatk --java-options "-Xmx{config[gatk][MarkIlluminaAdapters][memory]}" '
            'MarkIlluminaAdapters '
            '-I={input} '
            '-O={output.bam} '
            '-M={output.metric} '
            '--VERBOSITY={config[gatk][verbosity]} '
            '--TMP_DIR={paths[temp_dir]} '

rule sam_to_fastq:
    input:
        marked=paths['marked_bam']

    output:
        pipe(paths['marked_fastq'])

    singularity:
        paths['gatk-container']

    group: 'map_fastq'

    shell:
        'gatk --java-options "-Xmx{config[gatk][SamToFastq][memory]}" '
            'SamToFastq '
            '-I={input.marked} '
            '--FASTQ={output} '
            '--VERBOSITY={config[gatk][verbosity]} '
            '--TMP_DIR={paths[temp_dir]} '
            '{config[gatk][SamToFastq][options]}'

rule map_bam:
    input:
        marked=paths['marked_fastq']

    output:
        pipe(paths['temp_mapped_bam'])

    group: 'map_fastq'

    shell:
        'bwa mem '
            '{config[gatk][Bwa_Mem][options]} '
            '-p {paths[ref_genome]} '
            '{input.marked} > {output} '
            '2>/dev/null'

rule merge_bam:
    input:
        bam=paths['temp_mapped_bam'],
        ubam=paths['split_ubam']

    output:
        bam=temp(paths['mapped_bam']),
        bai=temp(paths['mapped_bai'])

    singularity:
        paths['gatk-container']

    group: 'map_fastq'

    shell:
        'gatk --java-options "-Xmx{config[gatk][MergeBamAlignment][memory]}" '
            'MergeBamAlignment '
            '--ALIGNED_BAM={input.bam} '
            '--UNMAPPED_BAM={input.ubam} '
            '--OUTPUT={output.bam} '
            '-R {paths[ref_genome]} '
            '--VERBOSITY={config[gatk][verbosity]} '
            '--TMP_DIR={paths[temp_dir]} '
            '{config[gatk][MergeBamAlignment][options]}'

def aggregate_bam_input(wildcards):
    if not os.path.exists(paths['split_ubam_dir'].format(id=wildcards.id)):
        checkpoints.split_ubam.get(**wildcards).output[0]

    split_ubam = paths['split_ubam'].replace('{id}', wildcards.id)
    result = expand(
            paths['mapped_bam'],
            id=wildcards.id,
            part=glob_wildcards(split_ubam).part)
    return result

def get_ubam_to_delete(wildcards):
    path = paths['split_ubam'].replace('{id}', wildcards.id)
    path = path.replace('{part}', '*')
    return path

rule merge_split_bam:
    input:
        aggregate_bam_input

    output:
        bam=temp(paths['mapped_bam_merged']),
        bai=temp(paths['mapped_bai_merged'])

    singularity:
        paths['gatk-container']

    params:
        ubams=get_ubam_to_delete

    group: 'merge_dedup'

    shell:
        'BAMS_ARG=()\n'
        'for bamfile in {input}; do\n'
            'BAMS_ARG+=(-I $bamfile)\n'
        'done\n'
        'gatk --java-options "-Xmx{config[gatk][MergeSamFilesSample][memory]}" '
            'MergeSamFiles '
            '--VERBOSITY={config[gatk][verbosity]} '
            '--ASSUME_SORTED '
            '-O {output.bam} '
            '${{BAMS_ARG[*]}} '
            '{config[gatk][MergeSamFilesSample][options]}\n'
        'rm {params.ubams} '

rule dedup_bam:
    input:
        bam=paths['mapped_bam_merged'],
        bai=paths['mapped_bai_merged'],
        ubam=paths['ubam']  # this prevents deletion of temp ubam

    output:
        bam=protected(paths['dedup_bam']),
        bai=protected(paths['dedup_bai']),
        metric=paths['dedup_metric']

    singularity:
        paths['gatk-container']

    group: 'merge_dedup'

    shell:
        'gatk --java-options "-Xmx{config[gatk][MarkDuplicates][memory]}" '
            'MarkDuplicates '
            '-I={input.bam} '
            '-O={output.bam} '
            '--METRICS_FILE={output.metric} '
            '--VERBOSITY={config[gatk][verbosity]} \n'
        'samtools index {output.bam}'
