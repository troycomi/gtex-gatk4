paths['mutect_pairs'] = paths['mutect_pairs'].format(
    name=paths['mutect_pair_name'])

with open(paths['mutect_pairs'], 'r') as reader:
    reader.readline()  # header
    tumor_normal = {}
    for line in reader:
        tokens = line.split()
        tumor_normal[tokens[0]] = tokens[1]

sample_details = get_samples(paths['sample_details'])
for contaminated in ('P6-B6', 'P6-A4'):
    if contaminated in sample_details:
        del sample_details[contaminated]

    if contaminated in tumor_normal:
        del tumor_normal[contaminated]
    items = list(tumor_normal.items())
    for k, v in items:
        if v == contaminated:
            del tumor_normal[k]

if config['center'] == 'merged':
    with open(paths['duplicate_pairs'], 'r') as reader:
        merge_map = {}
        reader.readline()  # header
        for line in reader:
            tokens = line.split()
            tumor_normal[tokens[2]] = tumor_normal[tokens[0]]
            merge_map[tokens[2]] = tokens[0]

if "subw_outputs_dict" in locals() and "ids" in locals():
    output_ids = [id for id in ids if id in tumor_normal]
    subw_outputs_dict['GATK-mutect2'] = expand(paths['filter_vcf'],
                                               id=output_ids,
                                               chromosome=chromosomes)
localrules:
    filter_variants,
    pon_mutect_args,
    generate_pon

rule sites_only:
    input:
        bam=ancient(paths['recal_chroms_bam']),
        ref=ancient(paths['ref_genome'])

    output:
        paths['sites_only']

    singularity:
        paths['gatk-container']

    resources:
        time=30*60,
        mem=4000

    shell:
        'SAMPLE=$(samtools view -H {input.bam} | '
            'grep "^@RG" | '
            'sed "s/.*SM:\([^\\t]*\).*/\\1/g" | '
            'sort | uniq ) \n'
        'if [[ $(echo $SAMPLE | wc -w) != 1 ]]; then \n'
            'echo {input.bam} contains more than 1 sample \n'
            'exit 1 \n'
        'fi \n'
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'Mutect2 '
            '--reference {input.ref} '
            '--input {input.bam} '
            '--tumor $SAMPLE '
            '--output {output} '
            '--verbosity {config[gatk][verbosity]} '

def get_pon_samples(wildcards):
    pon_ids = [detail.sample
               for detail in sample_details.values()
               if (detail.individual != wildcards.individual and
                    detail.center == wildcards.center)
               ]
    # remove duplicates from nwgc
    if wildcards.center == 'nwgc':
        pon_ids = [pon_id for pon_id in pon_ids if pon_id[-1] != 'd']
    return expand(paths['sites_only'],
                  id=pon_ids)

def get_pon_samples_WGS(wildcards):
    pon_ids = [detail.sample
            for detail in sample_details.values()
            if detail.individual != wildcards.individual
    ]
    pon_ids.append('P6-A4')  # add to all individuals
    return expand(paths['sites_only'],
                  id=pon_ids, chromosome=wildcards.chromosome)

rule pon_mutect_args:
    input:
        get_pon_samples_WGS

    output:
        temp(paths['pon_list'])

    run:
        with open(output[0], 'w') as outfile:
            for f in input:
                outfile.write(f + "\n")

rule generate_pon:
    input:
        pon=paths['pon_list']

    output:
        paths['pon']

    singularity:
        paths['gatk-container']

    resources:
        time=120,
        mem=1000,

    shell:
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'CreateSomaticPanelOfNormals '
            '-O {output} '
            '-vcfs {input} '
            '--verbosity {config[gatk][verbosity]} '

rule collect_counts:
    input:
        ancient(paths['recal_chroms_bam'])

    output:
        table=paths['mutect_alt'],
        ref_hist=paths['mutect_ref_hist'],
        alt_hist=paths['mutect_alt_hist']

    singularity:
        paths['gatk-container']

    group: 'call_variants'

    resources:
        time=40 * 60,
        mem=6000

    shell:
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'CollectF1R2Counts '
            '--reference {paths[ref_genome]} '
            '--input {input} '
            '-alt-table {output.table} '
            '-ref-hist {output.ref_hist} '
            '-alt-hist {output.alt_hist} '
            '--verbosity {config[gatk][verbosity]} '
            # '--intervals {paths[exome_intervals]} '

rule learn_model:
    input:
        table=ancient(paths['mutect_alt']),
        ref_hist=ancient(paths['mutect_ref_hist']),
        alt_hist=ancient(paths['mutect_alt_hist'])

    output:
        paths['mutect_prior']

    singularity:
        paths['gatk-container']

    group: 'call_variants'

    resources:
        time=40 * 60,
        mem=6000

    shell:
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'LearnReadOrientationModel '
            '-alt-table {input.table} '
            '-ref-hist {input.ref_hist} '
            '-alt-hist {input.alt_hist} '
            '-O {output} '
            '--verbosity {config[gatk][verbosity]} '

def get_call_input(wildcards):
    detail = sample_details[wildcards.id]
    param = get_call_params(wildcards)
    center = detail.center
    if center == 'merged':
        center = sample_details[merge_map[wildcards.id]].center
    result = {'pon': ancient(expand(paths['pon'],
                individual=detail.individual,
                chromosome=wildcards.chromosome)),
            'prior': ancient(expand(paths['mutect_prior'],
                id=wildcards.id,
                chromosome=wildcards.chromosome)),
            'test': ancient(expand(paths['recal_chroms_bam'],
                id=wildcards.id,
                chromosome=wildcards.chromosome)),
            'normal': ancient(expand(paths['recal_chroms_bam'],
                id=param['normal'],
                chromosome=wildcards.chromosome))}

    return result

def get_call_params(wildcards):
    '''
    return test and normal sample names from ids
    strip ending d for nwgc samples
    '''
    test_details = sample_details[wildcards.id]
    if wildcards.id not in tumor_normal:
        raise ValueError("No matching normal sample found for sample " +
                         wildcards.id)

    normal_details = sample_details[tumor_normal[wildcards.id]]
    test = test_details.sample
    if test_details.center == 'nwgc':
        test = test.rstrip('d')
    normal = normal_details.sample
    if normal_details.center == 'nwgc':
        normal = normal.rstrip('d')

    result = {'test': test, 'normal': normal}

    return result

rule call_variants:
    input:
        unpack(get_call_input)

    output:
        vcf=paths['unfiltered_vcf'],
        bam=paths['mutect_bam']

    params:
        names=get_call_params

    singularity:
        paths['gatk-container']

    group: 'call_variants'

    resources:
        time=40 * 60,
        mem=6000

    shell:
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'Mutect2 '
            '--reference {paths[ref_genome]} '
            '--input {input.test} '
            '--tumor {params.names[test]} '
            '--input {input.normal} '
            '--normal {params.names[normal]} '
            '--pon {input.pon} '
            '--output {output.vcf} '
            '-bamout {output.bam} '
            '--germline-resource {paths[germline_resource]} '
            '--orientation-bias-artifact-priors {input.prior} '
            '--verbosity {config[gatk][verbosity]} '
            '{config[gatk][Mutect2_Calls][options]} '
            # '--intervals {paths[exome_intervals]} '

rule filter_variants:
    input:
        bam=paths['recal_chroms_bam'],
        vcf=paths['unfiltered_vcf']

    output:
        pileup=paths['pileup'],
        con_tab=paths['con_tab'],
        vcf=paths['filter_vcf']

    singularity:
        paths['gatk-container']

    resources:
        time=10,
        mem=2000,

    shell:
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'GetPileupSummaries '
            '--input {input.bam} '
            '--output {output.pileup} '
            '--variant {paths[common_snps]} '
            '--intervals {paths[common_snps]} '
            '--verbosity {config[gatk][verbosity]} '
            '{config[gatk][Mutect2_Filter][pileup_options]} \n'
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'CalculateContamination '
            '--input {output.pileup} '
            '--output {output.con_tab} '
            '--verbosity {config[gatk][verbosity]} \n'
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'FilterMutectCalls '
            '--variant {input.vcf} '
            '--contamination-table {output.con_tab} '
            '--output {output.vcf} '
            '--stats /dev/null '
            '--verbosity {config[gatk][verbosity]} '
            '{config[gatk][Mutect2_Filter][filter_options]} \n'
