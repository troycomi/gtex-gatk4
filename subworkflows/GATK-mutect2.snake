paths['mutect_pairs'] = paths['mutect_pairs'].format(
    name=paths['mutect_pair_name'])

with open(paths['mutect_pairs'], 'r') as reader:
    reader.readline()  # header
    tumor_normal = {}
    for line in reader:
        tokens = line.split()
        tumor_normal[tokens[0]] = tokens[1]

sample_details = get_samples(paths['sample_details'])
for contaminated in ('P6-B6', 'P6-A4'):
    if contaminated in sample_details:
        del sample_details[contaminated]

    if contaminated in tumor_normal:
        del tumor_normal[contaminated]
    items = list(tumor_normal.items())
    for k, v in items:
        if v == contaminated:
            del tumor_normal[k]

if config['center'] == 'merged':
    with open(paths['duplicate_pairs'], 'r') as reader:
        merge_map = {}
        reader.readline()  # header
        for line in reader:
            tokens = line.split()
            tumor_normal[tokens[2]] = tumor_normal[tokens[0]]
            merge_map[tokens[2]] = tokens[0]

if "subw_outputs_dict" in locals() and "ids" in locals():
    output_ids = [id for id in ids if id in tumor_normal]
    subw_outputs_dict['GATK-mutect2'] = expand(paths['filter_vcf'],
                                               id=output_ids)
localrules:
    filter_variants,

rule sites_only:
    input:
        bam=ancient(paths['recal_bam']),
        ref=ancient(paths['ref_genome'])

    output:
        paths['sites_only']

    singularity:
        paths['gatk-container']

    resources:
        time=16*60,
        mem=4000

    shell:
        'SAMPLE=$(samtools view -H {input.bam} | '
            'grep "^@RG" | '
            'sed "s/.*SM:\([^\\t]*\).*/\\1/g" | '
            'sort | uniq ) \n'
        'if [[ $(echo $SAMPLE | wc -w) != 1 ]]; then \n'
            'echo {input.bam} contains more than 1 sample \n'
            'exit 1 \n'
        'fi \n'
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'Mutect2 '
            '--reference {input.ref} '
            '--input {input.bam} '
            '--max-mnp-distance 0 '
            '--output {output} '
            '--verbosity {config[gatk][verbosity]} '

def get_pon_samples(wildcards):
    pon_ids = [detail.sample
               for detail in sample_details.values()
               if (detail.individual != wildcards.individual and
                    detail.center == wildcards.center)
               ]
    # remove duplicates from nwgc
    if wildcards.center == 'nwgc':
        pon_ids = [pon_id for pon_id in pon_ids if pon_id[-1] != 'd']
    return expand(paths['sites_only'],
                  id=pon_ids)

def get_pon_samples_WGS(wildcards):
    pon_ids = [detail.sample
            for detail in sample_details.values()
            if detail.individual != wildcards.individual
    ]
    pon_ids.append('P6-A4')  # add to all individuals
    return expand(paths['sites_only'],
                  id=pon_ids, chromosome=wildcards.chromosome)

rule generate_pon_db:
    input:
        get_pon_samples

    output:
        temp(directory(paths['pon_db']))

    params:
        inputs=lambda wildcards, input: '-V ' + ' -V '.join(input)

    resources:
        time=24*60,
        mem=8000

    singularity:
        paths['gatk-container']

    shell:
        'gatk --java-options -Xmx$(({resources.mem} * 8 / 10))M '
            'GenomicsDBImport  '
            '--verbosity {config[gatk][verbosity]} '
            '-L {paths[exome_intervals]} '
            '--batch-size 50 '
            '--merge-input-intervals '
            '--genomicsdb-workspace-path {output} '
            '{params.inputs} '

rule generate_pon:
    input:
        pon=paths['pon_db']

    output:
        paths['pon']

    singularity:
        paths['gatk-container']

    resources:
        time=6*60,
        mem=4000,

    shell:
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'CreateSomaticPanelOfNormals '
            '--germline-resource {paths[germline_resource]} '
            '--reference {paths[ref_genome]} '
            '-O {output} '
            '-V gendb://{input} '
            '--verbosity {config[gatk][verbosity]} '

def get_call_input(wildcards):
    detail = sample_details[wildcards.id]
    param = get_call_params(wildcards)
    center = detail.center
    if center == 'merged':
        center = sample_details[merge_map[wildcards.id]].center
    result = {'pon': ancient(expand(paths['pon'],
                individual=detail.individual,
                center=center)),
            'test': ancient(expand(paths['recal_bam'],
                id=wildcards.id,
                )),
            'normal': ancient(expand(paths['recal_bam'],
                id=param['normal'],
                ))}

    return result

def get_call_params(wildcards):
    '''
    return test and normal sample names from ids
    strip ending d for nwgc samples
    '''
    test_details = sample_details[wildcards.id]
    if wildcards.id not in tumor_normal:
        raise ValueError("No matching normal sample found for sample " +
                         wildcards.id)

    normal_details = sample_details[tumor_normal[wildcards.id]]
    test = test_details.sample
    if test_details.center == 'nwgc':
        test = test.rstrip('d')
    normal = normal_details.sample
    if normal_details.center == 'nwgc':
        normal = normal.rstrip('d')

    result = {'test': test, 'normal': normal}

    return result

rule mutect2:
    input:
        unpack(get_call_input)

    output:
        vcf=paths['unfiltered_vcf'],
        f1r2=paths['mutect_f1r2']

    params:
        names=get_call_params

    singularity:
        paths['gatk-container']

    group: 'call_variants'

    resources:
        time=2 * 24 * 60,
        mem=8000

    shell:
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'Mutect2 '
            '--reference {paths[ref_genome]} '
            '--input {input.test} '
            '--tumor {params.names[test]} '
            '--input {input.normal} '
            '--normal {params.names[normal]} '
            '--pon {input.pon} '
            '--output {output.vcf} '
            '--germline-resource {paths[germline_resource]} '
            '--verbosity {config[gatk][verbosity]} '
            '--intervals {paths[exome_intervals]} '
            '--f1r2-tar-gz {output.f1r2} '
            '{config[gatk][Mutect2_Calls][options]} '

rule learn_model:
    input:
        paths['mutect_f1r2']

    output:
        paths['mutect_read_model']

    singularity:
        paths['gatk-container']

    group: 'call_variants'

    resources:
        time=2 * 24 * 60,
        mem=8000

    shell:
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'LearnReadOrientationModel '
            '--input {input} '
            '--output {output} '
            '--verbosity {config[gatk][verbosity]} '

rule filter_variants:
    input:
        bam=paths['recal_bam'],
        vcf=paths['unfiltered_vcf'],
        model=paths['mutect_read_model']

    output:
        pileup=paths['pileup'],
        con_tab=paths['con_tab'],
        tumor_tab=paths['tumor_segments'],
        vcf=paths['filter_vcf']

    singularity:
        paths['gatk-container']

    resources:
        time=63,
        mem=2000,

    shell:
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'GetPileupSummaries '
            '--input {input.bam} '
            '--output {output.pileup} '
            '--variant {paths[common_snps]} '
            '--intervals {paths[common_snps]} '
            '--verbosity {config[gatk][verbosity]} '
            '{config[gatk][Mutect2_Filter][pileup_options]} \n'
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'CalculateContamination '
            '--input {output.pileup} '
            '--output {output.con_tab} '
            '--tumor-segmentation {output.tumor_tab} '
            '--verbosity {config[gatk][verbosity]} \n'
        'gatk --java-options -Xmx$(({resources.mem} * 9 / 10))M '
            'FilterMutectCalls '
            '--variant {input.vcf} '
            '--reference {paths[ref_genome]} '
            '--output {output.vcf} '
            '--contamination-table {output.con_tab} '
            '--orientation-bias-artifact-priors {input.model} '
            '--tumor-segmentation {output.tumor_tab} '
            '--verbosity {config[gatk][verbosity]} '
            '{config[gatk][Mutect2_Filter][filter_options]} \n'
